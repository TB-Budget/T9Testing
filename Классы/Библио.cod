--{{ Т9_Тесты - проект для тестирования проектов, созданных на платформе Т9 (c) ТБ.Бюджет, 2015

--Данный проект является свободным программным обеспечением. Вы вправе распространять его
--и/или модифицировать в соответствии с условиями версии 2.1 либо по вашему выбору с условиями
--более поздней версии Стандартной Общественной Лицензии Ограниченного Применения GNU,
--опубликованной Free Software Foundation.

--Мы распространяем этот проект в надежде на то, что он будет вам полезен, однако
--НЕ ПРЕДОСТАВЛЯЕМ НА НЕГО НИКАКИХ ГАРАНТИЙ, в том числе ГАРАНТИИ ТОВАРНОГО СОСТОЯНИЯ ПРИ ПРОДАЖЕ
--и ПРИГОДНОСТИ ДЛЯ ИСПОЛЬЗОВАНИЯ В КОНКРЕТНЫХ ЦЕЛЯХ. Для получения более подробной информации
--ознакомьтесь со Стандартной Общественной Лицензией Ограниченного Применений GNU.

--Вместе с данным проектом вы должны были получить экземпляр Стандартной Общественной Лицензии
--Ограниченного Применения GNU. Если вы его не получили, сообщите об этом в Software Foundation, Inc.,
--59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
--}

class "";

inclass public

  --@doc Функция возврвщает целочисленную константу, определяющую тип объекта:
  func ТипТестируемогоКласса(Cl: Class): Константы.типБазовыйКласс;
    var InfoRec: MethodInfo;
    if Cl.InheritsFrom(BlankForm):
      InfoRec = Cl.ClassInfo.MemberByName('Record') as MethodInfo;
      if (InfoRec <> nil):
        Return Константы.ТипБланкРедактор;
      else
        Return Константы.ТипБланк;
      fi;
    elsif Cl.InheritsFrom(CardForm):
      Return Константы.ТипКартотека;
    elsif Cl.InheritsFrom(ReportForm):
      Return Константы.ТипОтчет;
    fi;
  end;

  --@doc Создает список в поле перечислимого типа на основании константы - многомерного массива,
  --@doc по умолчанию имеющего структуру [[Код:integer,Наименование:string]]
  --@doc Метод скопирован из проекта ТББ_Базовый
  proc ОбновитьСписокПоля(locCell: TemplateCell; locList: variant[2]; ИндексКода: integer = 1; ИндексОписания: integer = 2);
    var i, ii: integer;
    if (locCell <> nil):
      locCell.List.Clear;
      ii = LengthOfArray(locList);
      for i = 1 .. ii do
        locCell.List.Add(locList[i,ИндексОписания] as String + '|' + Str(locList[i,ИндексКода]));
      od;
    fi;
  end;

  --@doc То же для столбцов картотеки
  proc ОбновитьСписокКартПоля(locColumn: CardFileColumn; locList: variant[2]; ИндексКода: integer = 1; ИндексОписания: integer = 2);
    var i, ii: integer;
    if (locColumn <> nil):
      locColumn.List.Clear;
      ii = LengthOfArray(locList);
      for i = 1 .. ii do
        locColumn.List.Add(locList[i,ИндексОписания] as String + '|' + Str(locList[i,ИндексКода]));
      od;
    fi;
  end;

  --@doc Ф-я возвращает описание класса по его имени.
  --@doc Если класс ПРИСУТСТВУЕТ в инф.базе сессии, то вычисляется его актуальное опиание.
  --@doc Если класс ОТСУТСТВУЕТ в инф.базе сессии, то описание считывается из записи спецификации.
  func ОписаниеКлассаПоИмени(locFullName: string): string;
    var vClass: Class;
    var vRecord: Т9_Тесты.СпецификацияМодуля;
    vClass = if(locFullName <> nil, FindClass(locFullName), nil);
    if (vClass <> nil):
      Result = vClass.ClassInfo.Description;
    else
      vRecord = СИС2.ФункцииДокумента.QueryRecord(Т9_Тесты.СпецификацияМодуля, 'ПолноеИмяКласса="' + locFullName + '"', 'ПолноеИмяКласса');
      if (vRecord <> nil):
        Result = vRecord.НаимКласса;
      fi;
    fi;
  end;

end